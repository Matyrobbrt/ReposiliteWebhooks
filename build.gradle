import java.nio.file.Files

plugins {
    id 'java'
    id 'idea'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

archivesBaseName = 'reposilite-webhooks'
group = 'com.matyrobbrt.reposilitewebhooks'

application {
    mainClass.set('com.reposilite.ReposiliteLauncherKt')
}
tasks.named('run', JavaExec).configure {
    it.setWorkingDir(project.file('run'))
    it.dependsOn(shadowJar)
    it.doFirst {
        final outDir = project.file('run/plugins').toPath()
        Files.createDirectories(outDir)
        final out = outDir.resolve('webhooks.jar')
        Files.deleteIfExists(out)
        Files.copy(shadowJar.archiveFile.get().asFile.toPath(), out)
    }
}

repositories {
    mavenCentral()
    maven {
        url = 'https://maven.reposilite.com/releases'
    }
    maven {
        url = 'https://maven.reposilite.com/snapshots'
    }
}

configurations {
    shade
    implementation.extendsFrom(shade)
}

dependencies {
    compileOnly('com.reposilite:reposilite:3.0.2')
    runtimeOnly('com.reposilite:reposilite:3.0.2:all') {
        exclude module: 'reposilite-frontend'
    }

    shade 'com.google.code.gson:gson:2.9.1'
    shade 'club.minnced:discord-webhooks:0.8.2'
}

shadowJar {
    configurations = [project.configurations.shade]
}